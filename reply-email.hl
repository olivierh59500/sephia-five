
/*
 * Allows user to create a reply to an email.
 *
 * Expects [email-id] being ID of email from database.
 */





/*
 * Finding main "reader widget" for given email, such that we can "replace it" with a "reply widget".
 */
p5.web.widgets.find:sephia-read-emails
  _email-id:x:/../*/email-id?value





/*
 * Opening up database.
 */
p5.mysql.connect:[sephia]

  /*
   * Selecting email data from database.
   */
  p5.mysql.select:@"select * from email 
inner join contact on email.sender = contact.id 
inner join part on part.emailid = email.id 
where email.id = @emailid and part.type='plain'"
    @username:x:/@whoami/*/username?value
    @emailid:x:/../*/email-id?value

  /*
   * Creating our "reply widget".
   * Making sure we get the "Re: " parts in subject.
   */
  set:x:/@p5.mysql.select/0/*/subject?value
    src:"Re: {0}"
      :x:/@p5.mysql.select/0/*/subject?value
  p5.html.html-encode:x:/@p5.mysql.select/0/*/subject?value

  /*
   * Making sure we get each line prepended with a ">".
   */
  replace:>{0}
    :x:/@p5.mysql.select/0/*/content?value
    src:"\r\n"
    dest:"\r\n>"
  trim-right:x:/@replace?value
    chars:">"
  p5.html.html-encode:x:/@trim-right?value

  /*
   * Creating our actual widget.
   */
  create-widget
    after:x:/@p5.web.widgets.find/*/*?value
    _email-id:x:/../*/email-id?value
    _is-reply:true
    class:sephia-email-reply col-xs-12
    widgets
      input
        type:text
        class:form-control sephia-reply-subject
        oninit
          p5.web.send-javascript:@"$('#{0}').focus().select();"
            :x:/../*/_event?value
        value:x:/@p5.mysql.connect/*/p5.html.html-encode/[0,1]?value
      textarea
        rows:19
        class:form-control
        innerValue:x:/@p5.mysql.connect/*/p5.html.html-encode/[1,2]?value
      div
        class:text-right prepend-top
        widgets
          button
            class:btn btn-default
            innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
            onclick

              /*
               * Asking user to confirm that he wants to discard his reply.
               */
              eval-x:x:/+/**/_widget
              sys42.windows.confirm
                header:Please confirm action
                body:@"<p>Are you sure you wish to discard your reply?</p>"
                .onok

                  /*
                   * Deleting "reply widget" and removing "open" CSS class on datagrid.
                   */
                  _widget:x:/../*/_event?value
                  p5.web.widgets.find-first-ancestor:x:/@_widget?value
                    _is-reply
                  p5.web.widgets.property.get:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                    _email-id
                  p5.web.widgets.find:sephia-datagrid
                    _email-id:x:/@p5.web.widgets.property.get/*/*?value
                  sys42.utilities.delete-css-classes:x:/@p5.web.widgets.find/*/*?value
                    class:sephia-open
                  delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value

  /*
   * Deleting "reader widget".
   */
  delete-widget:x:/@p5.web.widgets.find/*/*?value
