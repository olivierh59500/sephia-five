
/*
 * Allows user to create a reply to an email.
 *
 * Expects [email-id] being ID of email from database.
 *
 * If no [email-id] is given, 
 */





/*
 * Finding main "reader widget" for given email, such that we can "replace it" with a "reply widget".
 */
p5.web.widgets.find:sephia-read-emails
  _email-id:x:/../*/email-id?value





/*
 * Opening up database.
 */
p5.mysql.connect:[sephia]

  /*
   * Selecting email data from database.
   */
  p5.mysql.select:@"select * from email 
inner join contact on email.sender = contact.id 
inner join part on part.emailid = email.id 
where email.id = @emailid and part.type='plain'"
    @username:x:/@whoami/*/username?value
    @emailid:x:/../*/email-id?value

  /*
   * Creating our "reply widget".
   * Making sure we get the "Re: " parts in subject, but only if it does NOT exist from before.
   */
  if
    fetch:x:/0/0?value
      index-of:x:/@p5.mysql.select/0/*/subject?value
        src:"Re:"
    =:int:0
    not
    set:x:/@p5.mysql.select/0/*/subject?value
      src:"Re: {0}"
        :x:/@p5.mysql.select/0/*/subject?value

  /*
   * Making sure we avoid "HTML injection", also in subject of email!
   */
  p5.html.html-encode:x:/@p5.mysql.select/0/*/subject?value

  /*
   * Making sure we get each line prepended with a ">".
   */
  replace:>{0}
    :x:/@p5.mysql.select/0/*/content?value
    src:"\r\n"
    dest:"\r\n>"
  trim-right:x:/@replace?value
    chars:">"
  p5.html.html-encode:x:/@trim-right?value

  /*
   * Retrieving recipients of original email.
   */
  p5.mysql.select:@"select * from contact inner join email on email.sender = contact.id where email.id = @emailid"
    @emailid:x:/../*/email-id?value
  eval-x:x:/+
  _to:x:/@p5.mysql.select/*/*/email?value

  /*
   * Then Cc (and Bcc, which technically shouldn't exist though for most cases).
   */
  p5.mysql.select:@"select * from contact inner join recipient on contact.id = recipient.contactid where recipient.emailid = @emailid"
    @emailid:x:/../*/email-id?value
  _cc
  for-each:x:/@p5.mysql.select/*/*/type/=Cc/./*/email?value
    set:x:/@_cc?value
      src:{0}, {1}
        :x:/@_cc?value
        :x:/@_dp?value
  trim-left:x:/@_cc?value
    chars:", "
  _bcc
  for-each:x:/@p5.mysql.select/*/*/type/=Bcc/./*/email?value
    set:x:/@_bcc?value
      src:{0}, {1}
        :x:/@_bcc?value
        :x:/@_dp?value
  trim-left:x:/@_bcc?value
    chars:", "

  /*
   * Creating our actual widget.
   */
  create-widget
    after:x:/@p5.web.widgets.find/*/*?value
    _email-id:x:/../*/email-id?value
    _is-reply:true
    class:sephia-email-reply col-xs-12
    widgets

      /*
       * Attachments "drag'n'drop" widget.
       */
      sys42.widgets.uploader
        filter:hl|txt|zip
        allow-multiple:true
        class:sephia-uploader
        clickable:false
        .onupload

          /*
           * Creating a unique filename, and saving file to private documents folder.
           */
          p5.types.guid.new
          replace:x:/@p5.types.guid.new?value.string
            src:-
          save-file:~/documents/private/sephia/attachments/sent-{0}-{1}
            :x:/@replace?value
            :x:/../*/_filename?value
            src:x:/../*/_content?value

          /*
           * Creating an "attachment widget" and adding into "reply widget".
           */
          p5.web.widgets.find-first-ancestor:x:/../*/_event?value
            _is-reply
          p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
            _attachment-wrapper
          create-widget
            parent:x:/@p5.web.widgets.find/*/*?value
            class:sephia-attachment
            _reply-attachment
            _folder:~/documents/private/sephia/attachments/
              :x:/@replace?value
              :x:/../*/_filename?value
            _filename:x:/../*/_filename?value
            _prefix:sent-{0}-
              :x:/@replace?value
            widgets
              span
                innerValue:x:/../*/_filename?value
              literal
                element:span
                role:button
                class:glyphicon glyphicon-trash sephia-delete-attachment
                onclick

                  /*
                   * Figuring out filename, and deleting file in its entirety, before deleting "attachment widget".
                   */
                  p5.web.widgets.get-parent:x:/../*/_event?value
                  p5.web.widgets.property.get:x:/@p5.web.widgets.get-parent/*/*?value
                    _folder
                    _filename
                    _prefix
                  delete-file:{0}{1}{2}
                    :x:/@p5.web.widgets.property.get/*/*/_folder?value
                    :x:/@p5.web.widgets.property.get/*/*/_prefix?value
                    :x:/@p5.web.widgets.property.get/*/*/_filename?value
                  delete-widget:x:/@p5.web.widgets.get-parent/*/*?value

      /*
       * Subject of email.
       */
      input
        type:text
        class:form-control sephia-reply-subject
        _subject
        oninit
          p5.web.send-javascript:@"$('#{0}').focus().select();"
            :x:/../*/_event?value
        value:x:/@p5.mysql.connect/*/p5.html.html-encode/[0,1]?value

      /*
       * Body of email.
       */
      textarea
        rows:13
        class:form-control
        _body
        innerValue:x:/@p5.mysql.connect/*/p5.html.html-encode/[1,2]?value

      /*
       * Recipients of email.
       */
      div
        class:prepend-top row
        widgets
          div
            class:col-xs-12 col-md-4 prepend-bottom
            widgets
              div
                class:input-group
                _to-field
                widgets
                  span
                    class:input-group-addon
                    innerValue:To
                  input
                    type:text
                    class:form-control sephia-recipient
                    placeholder:To ...
                    value:x:/@_to?value
                    _to-field-to
                  span
                    class:input-group-btn
                    widgets
                      button
                        class:btn btn-default
                        innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
                        onclick

                          /*
                           * Removing entire value from textbox widget, and setting focus to textbox.
                           */
                          p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                            _to-field
                          p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                            element:input
                          p5.web.widgets.property.set:x:/@p5.web.widgets.find/*/*?value
                            value:
                          p5.web.send-javascript:@"$('#{0}').focus();"
                            :x:/@p5.web.widgets.find/*/*?value

                      button
                        class:btn btn-default
                        innerValue:@"<span class=""glyphicon glyphicon-plus""></span>"
                        onclick

                          /*
                           * Executing file that is reponsible for retrieving a new email address from "contact" table,
                           * making sure we pass in widget ID for widget where result should be added.
                           */
                          p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                            _to-field
                          p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                            element:input
                          eval-x:x:/+/*
                          sys42.utilities.execute-lambda-file:@SEPHIA/add-recipient.hl
                            widget:x:/@p5.web.widgets.find/*/*?value

          div
            class:col-xs-12 col-md-4 prepend-bottom
            widgets
              div
                class:input-group
                _to-field
                widgets
                  span
                    class:input-group-addon
                    innerValue:Cc
                  input
                    type:text
                    class:form-control sephia-recipient
                    placeholder:Cc ...
                    value:x:/..p5.mysql.connect/*/trim-left/[0,1]?value
                    _to-field-cc
                  span
                    class:input-group-btn
                    widgets
                      button
                        class:btn btn-default
                        innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
                        onclick

                          /*
                           * Removing entire value from textbox widget, and setting focus to textbox.
                           */
                          p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                            _to-field
                          p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                            element:input
                          p5.web.widgets.property.set:x:/@p5.web.widgets.find/*/*?value
                            value:
                          p5.web.send-javascript:@"$('#{0}').focus();"
                            :x:/@p5.web.widgets.find/*/*?value
                      button
                        class:btn btn-default
                        innerValue:@"<span class=""glyphicon glyphicon-plus""></span>"
                        onclick

                          /*
                           * Executing file that is reponsible for retrieving a new email address from "contact" table,
                           * making sure we pass in widget ID for widget where result should be added.
                           */
                          p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                            _to-field
                          p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                            element:input
                          eval-x:x:/+/*
                          sys42.utilities.execute-lambda-file:@SEPHIA/add-recipient.hl
                            widget:x:/@p5.web.widgets.find/*/*?value

          div
            class:col-xs-12 col-md-4 prepend-bottom
            widgets
              div
                class:input-group
                _to-field
                widgets
                  span
                    class:input-group-addon
                    innerValue:Bcc
                  input
                    type:text
                    class:form-control sephia-recipient
                    placeholder:Bcc ...
                    value:x:/..p5.mysql.connect/*/trim-left/[1,2]?value
                    _to-field-bcc
                  span
                    class:input-group-btn
                    widgets
                      button
                        class:btn btn-default
                        innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
                        onclick

                          /*
                           * Removing entire value from textbox widget, and setting focus to textbox.
                           */
                          p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                            _to-field
                          p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                            element:input
                          p5.web.widgets.property.set:x:/@p5.web.widgets.find/*/*?value
                            value:
                          p5.web.send-javascript:@"$('#{0}').focus();"
                            :x:/@p5.web.widgets.find/*/*?value
                      button
                        class:btn btn-default
                        innerValue:@"<span class=""glyphicon glyphicon-plus""></span>"
                        onclick

                          /*
                           * Executing file that is reponsible for retrieving a new email address from "contact" table,
                           * making sure we pass in widget ID for widget where result should be added.
                           */
                          p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                            _to-field
                          p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                            element:input
                          eval-x:x:/+/*
                          sys42.utilities.execute-lambda-file:@SEPHIA/add-recipient.hl
                            widget:x:/@p5.web.widgets.find/*/*?value

      /*
       * Attachment widget.
       */
      container
        _attachment-wrapper
        class:sephia-attachments
      literal
        element:div
        class:clearfix

      /*
       * "Action buttons" (send, discard).
       */
      div
        class:text-right
        widgets
          div
            class:btn-group
            role:group
            widgets
              button
                class:btn btn-default sephia-action-button
                innerValue:@"<span class=""glyphicon glyphicon-send""></span>"
                title:Send reply
                onclick

                  /*
                   * Retrieving main "root widget" for reply surface.
                   */
                  p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                    _is-reply

                  /*
                   * Retrieving "To", "Cc" and "Bcc" values, and splitting them upon ",", to separate the different recipients.
                   * First "To".
                   */
                  p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                    _to-field-to
                  _to
                  p5.web.widgets.property.get:x:/@p5.web.widgets.find/*/*?value
                    value

                  /*
                   * Then "Cc".
                   */
                  p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                    _to-field-cc
                  _cc
                  p5.web.widgets.property.get:x:/@p5.web.widgets.find/*/*?value
                    value

                  /*
                   * Then "Bcc".
                   */
                  p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                    _to-field-bcc
                  _bcc
                  p5.web.widgets.property.get:x:/@p5.web.widgets.find/*/*?value
                    value

                  /*
                   * Then retrieving Subject.
                   */
                  p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                    _subject
                  _subject
                  p5.web.widgets.property.get:x:/@p5.web.widgets.find/*/*?value
                    value

                  /*
                   * Then retrieving Body.
                   */
                  p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                    _body
                  _body
                  p5.web.widgets.property.get:x:/@p5.web.widgets.find/*/*?value
                    value

                  /*
                   * Then retrieving SMTP settings.
                   */
                  p5.auth.my-settings.get

                  /*
                   * Adding recipients.
                   */
                  whoami
                  p5.mysql.connect:[sephia]

                    /*
                     * Adding "To" field.
                     */
                    split:x:/../*/_to/+/*/*?value
                      =:,
                      trim:true
                    for-each:x:/@split/*?name
                      p5.mysql.select:@"select name, email from contact where username = @username and email = @email"
                        @username:x:/@whoami/*/username?value
                        @email:x:/@_dp?value
                      if:x:/@p5.mysql.select/*
                        add:x:/../*/p5.smtp.send/*/envelope/*/To
                          src:"{0}:{1}"
                            :x:/@p5.mysql.select/*/*/name?value
                            :x:/@p5.mysql.select/*/*/email?value
                      else
                        add:x:/../*/p5.smtp.send/*/envelope/*/To
                          src:"Anonymous:{0}"
                            :x:/@_dp?value

                    /*
                     * Adding "Cc" field.
                     */
                    split:x:/../*/_cc/+/*/*?value
                      =:,
                      trim:true
                    for-each:x:/@split/*?name
                      p5.mysql.select:@"select name, email from contact where username = @username and email = @email"
                        @username:x:/@whoami/*/username?value
                        @email:x:/@_dp?value
                      if:x:/@p5.mysql.select/*
                        add:x:/../*/p5.smtp.send/*/envelope/*/Cc
                          src:"{0}:{1}"
                            :x:/@p5.mysql.select/*/*/name?value
                            :x:/@p5.mysql.select/*/*/email?value
                      else
                        add:x:/../*/p5.smtp.send/*/envelope/*/Cc
                          src:"Anonymous:{0}"
                            :x:/@_dp?value

                    /*
                     * Adding "Bcc" field.
                     */
                    split:x:/../*/_bcc/+/*/*?value
                      =:,
                      trim:true
                    for-each:x:/@split/*?name
                      p5.mysql.select:@"select name, email from contact where username = @username and email = @email"
                        @username:x:/@whoami/*/username?value
                        @email:x:/@_dp?value
                      if:x:/@p5.mysql.select/*
                        add:x:/../*/p5.smtp.send/*/envelope/*/Bcc
                          src:"{0}:{1}"
                            :x:/@p5.mysql.select/*/*/name?value
                            :x:/@p5.mysql.select/*/*/email?value
                      else
                        add:x:/../*/p5.smtp.send/*/envelope/*/Bcc
                          src:"Anonymous:{0}"
                            :x:/@_dp?value

                  /*
                   * Adding "From".
                   */
                  add:x:/../*/p5.smtp.send/*/envelope/*/From
                    src:"{0}:{1}"
                      :x:/@p5.auth.my-settings.get/*/sephia/*/name?value
                      :x:/@p5.auth.my-settings.get/*/sephia/*/email?value

                  /*
                   * Adding attachments.
                   */
                  p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                    _reply-attachment
                  p5.web.widgets.property.get:x:/@p5.web.widgets.find/*/*?value
                    _folder
                    _filename
                    _prefix
                  apply:x:/../*/sys42.utilities.execute-lambda-file
                    src:x:/@p5.web.widgets.property.get/*
                    template
                      file
                        {folder}:x:/*/_folder?value
                        {filename}:x:/*/_filename?value
                        {prefix}:x:/*/_prefix?value
                  for-each:x:/@p5.web.widgets.property.get/*
                    split:x:/@_dp/#/*/_filename?value
                      =:.
                    switch:x:/@split/0/-?name
                      case:txt
                      case:md
                        eval-x:x:/+/*/*/*(/Content-Disposition|/filename)
                        add:x:/../*/p5.smtp.send/*/envelope/*/body/*/multipart
                          src
                            text:plain
                              Content-Disposition:@"attachment; filename=""{0}"""
                                :x:/@_dp/#/*/_filename?value
                              filename:{0}{1}{2}
                                :x:/@_dp/#/*/_folder?value
                                :x:/@_dp/#/*/_prefix?value
                                :x:/@_dp/#/*/_filename?value
                      case:zip
                        eval-x:x:/+/*/*/*(/Content-Disposition|/filename)
                        add:x:/../*/p5.smtp.send/*/envelope/*/body/*/multipart
                          src
                            application:zip
                              Content-Disposition:@"attachment; filename=""{0}"""
                                :x:/@_dp/#/*/_filename?value
                              Content-Transfer-Encoding:binary
                              filename:{0}{1}{2}
                                :x:/@_dp/#/*/_folder?value
                                :x:/@_dp/#/*/_prefix?value
                                :x:/@_dp/#/*/_filename?value
                      case:png
                        eval-x:x:/+/*/*/*(/Content-Disposition|/filename)
                        add:x:/../*/p5.smtp.send/*/envelope/*/body/*/multipart
                          src
                            image:png
                              Content-Disposition:@"attachment; filename=""{0}"""
                                :x:/@_dp/#/*/_filename?value
                              Content-Transfer-Encoding:binary
                              filename:{0}{1}{2}
                                :x:/@_dp/#/*/_folder?value
                                :x:/@_dp/#/*/_prefix?value
                                :x:/@_dp/#/*/_filename?value
                      case:jpg
                      case:jpeg
                        eval-x:x:/+/*/*/*(/Content-Disposition|/filename)
                        add:x:/../*/p5.smtp.send/*/envelope/*/body/*/multipart
                          src
                            image:jpeg
                              Content-Disposition:@"attachment; filename=""{0}"""
                                :x:/@_dp/#/*/_filename?value
                              Content-Transfer-Encoding:binary
                              filename:{0}{1}{2}
                                :x:/@_dp/#/*/_folder?value
                                :x:/@_dp/#/*/_prefix?value
                                :x:/@_dp/#/*/_filename?value
                      case:gif
                        eval-x:x:/+/*/*/*(/Content-Disposition|/filename)
                        add:x:/../*/p5.smtp.send/*/envelope/*/body/*/multipart
                          src
                            image:gif
                              Content-Disposition:@"attachment; filename=""{0}"""
                                :x:/@_dp/#/*/_filename?value
                              Content-Transfer-Encoding:binary
                              filename:{0}{1}{2}
                                :x:/@_dp/#/*/_folder?value
                                :x:/@_dp/#/*/_prefix?value
                                :x:/@_dp/#/*/_filename?value
                      case:hl
                        eval-x:x:/+/*/*/*(/Content-Disposition|/filename)
                        add:x:/../*/p5.smtp.send/*/envelope/*/body/*/multipart
                          src
                            application:x-hyperlambda
                              Content-Disposition:@"attachment; filename=""{0}"""
                                :x:/@_dp/#/*/_filename?value
                              filename:{0}{1}{2}
                                :x:/@_dp/#/*/_folder?value
                                :x:/@_dp/#/*/_prefix?value
                                :x:/@_dp/#/*/_filename?value

                  /*
                   * Then verifying we have a public PGP key for all recipients, 
                   * but only doing this if page value has not "overridden" check.
                   */
                  _encrypt:bool:false
                  p5.web.viewstate.get:sephia.force-send
                  if:x:/@p5.web.viewstate.get/*
                    not
                    p5.crypto.list-public-keys
                    for-each:x:/../*/p5.smtp.send/*/envelope/*(/To|/Cc|/Bcc)/*?value
                      if:x:@"/@p5.crypto.list-public-keys/*/~{0}"
                        :"<{0}>"
                          :x:/@_dp?value
                        not

                        /*
                         * Warning user, asking to confirm sending, before allowing email to be sent without encryption!
                         */
                        eval-x:x:/+/**/_send-widget
                        sys42.windows.confirm
                          header:WARNING!!
                          body:@"<p>You don't seem to have an encryption certificate for all recipients in your email, do you still wish to proceed?</p>"
                          .onok
                            _send-widget:x:/../*/_event?value
                            p5.web.viewstate.set:sephia.force-send
                              src:true
                            p5.web.widgets.ajax-events.raise:x:/@_send-widget?value
                              onclick
                        return

                    /*
                     * If we've come this far, we have public PGP keys for all recipients.
                     */
                    set:x:/@_encrypt?value
                      src:bool:true

                  /*
                   * Making sure we remove "signal value" used in above logic.
                   */
                  p5.web.viewstate.set:sephia.force-send

                  /*
                   * Checking to see if we should encrypt email.
                   */
                  if:x:/@_encrypt?value

                    /*
                     * Yup, encrypting!
                     */
                    add:x:/+/*/*
                      src:x:/../*/p5.smtp.send/*/envelope/*(/To|/Cc|/Bcc)/*
                    add:x:/../*/p5.smtp.send/*/envelope/*/body/*/multipart
                      src
                        encrypt
                    set:x:/../*/p5.smtp.send/*/envelope/*/body/*/multipart/*/encrypt/*?name
                      src:email
                    add:x:/../*/sys42.utilities.execute-lambda-file
                      src
                        encrypted:int:1

                  /*
                   * Constructing and sending email.
                   */
                  eval-x:x:/../*/p5.smtp.send/**
                  add:x:/../*/sys42.utilities.execute-lambda-file
                    src:x:/../*/p5.smtp.send/*/envelope
                  sys42.utilities.execute-lambda-file:@SEPHIA/save-envelope-to-database.hl
                  p5.smtp.send
                    envelope
                      To
                      Cc
                      Bcc
                      From
                      Subject:x:/../**/_subject/+/*/*?value
                      body
                        multipart:mixed
                          sign
                            email:x:/@p5.auth.my-settings.get/*/sephia/*/email?value
                              password:x:/@p5.auth.my-settings.get/*/sephia/*/gpg-password?value
                          text:plain
                            content:@"{0}

{1}
{2}"
                              :x:/../**/_body/+/*/*?value
                              :"-- "
                              :x:/@p5.auth.my-settings.get/*/sephia/*/signature?value

                  /*
                   * Deleting "reply widget", and notify user.
                   */
                  delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  sys42.windows.info-tip:Email was successfully sent!
                  clear-widget:sephia-datagrid
                  sys42.utilities.execute-lambda-file:@SEPHIA/show-inbox.hl


              button
                class:btn btn-default sephia-action-button
                innerValue:@"<span class=""glyphicon glyphicon-trash""></span>"
                title:Discard reply
                onclick

                  /*
                   * Asking user to confirm that he wants to discard his reply.
                   */
                  eval-x:x:/+/**/_widget
                  sys42.windows.confirm
                    header:Please confirm action
                    body:@"<p>Are you sure you wish to discard your reply?</p>"
                    .onok

                      /*
                       * Deleting "reply widget" and removing "open" CSS class on datagrid.
                       */
                      _widget:x:/../*/_event?value
                      p5.web.widgets.find-first-ancestor:x:/@_widget?value
                        _is-reply
                      p5.web.widgets.property.get:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                        _email-id
                      p5.web.widgets.find:sephia-datagrid
                        _email-id:x:/@p5.web.widgets.property.get/*/*?value
                      sys42.utilities.delete-css-classes:x:/@p5.web.widgets.find/*/*?value
                        class:sephia-open
                      delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value

  /*
   * Deleting "reader widget".
   */
  delete-widget:x:/@p5.web.widgets.find/*/*?value
