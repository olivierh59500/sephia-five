
/*
 * Creates the main GUI structure of Sephia Mail.
 *
 * Sets a couple of properties on the page, in addition to creating the main "root widgets", such as the
 * "inbox widget" and the "toolbar widget", for then to initially populate the "inbox datagrid".
 */





/*
 * Creating header and "menu" row.
 */
create-widget:sephia-header
  parent:cnt
  class:row
  events

    /*
     * Event raised when datagrid needs re-databinding.
     *
     * Expects optionally [offset] and [filter], simply passes these two values into evaluation of Hyperlambda file,
     * responsible for re-databinding "inbox".
     */
    sephia.inbox.databind

      /*
       * Invoking file responsible for re-databinding "inbox".
       *
       * First making sure we delete old widgets.
       */
      clear-widget:sephia-datagrid
      add:x:/+
        src:x:/../*(/filter|/offset)
      sys42.utilities.execute-lambda-file:@SEPHIA/inbox/show.hl

  widgets
    div:sephia-main-header
      class:col-xs-12 sephia-brand
      widgets

        /*
         * Header at the top, feel free to add whatever "branding" widgets you wish here, 
         * replacing the existing widget.
         */
        a
          href:"https://gaiasoul.com/donate"
          target:_blank
          widgets
            h1
              innerValue:"Sephia Mail"
              title:Hide your ink!

    /*
     * This is our main "toolbar" (the thing that drops down at the top of the page, and stays in a fixed position).
     */
    div
      class:sephia-selector
      widgets

        div
          class:col-xs-8
          widgets

            /*
             * This is the "search and page" toolbar at the top.
             *
             * Notice, we wrap it inside the "sephia-selector" CSS class widget, to make it "drop down" during start,
             * and stay in a fixed position during browsing.
             */
            sephia.widgets.search-toolbar
              databind-event:sephia.inbox.databind
              page-size:int:19
              search-accesskey:F

        /*
         * Wrapper for "new email" button.
         */
        div
          class:col-xs-2
          widgets
            button
              innerValue:@"<span class=""glyphicon glyphicon-envelope""></span>"
              class:btn btn-success sephia-new-email-btn
              onclick

                /*
                 * Creating a new email, and making sure we scroll widget for creating it into view.
                 */
                sys42.utilities.execute-lambda-file:@SEPHIA/send/create.hl
                p5.web.widgets.find:sephia-read-emails
                  _email-id:x:/../*/email-id?value.string
                p5.web.send-javascript:@"$('#{0}')[0].scrollIntoView();"
                  :x:/@p5.web.widgets.find/*/*?value

        /*
         * Wrapper for "configure settings" button.
         */
        div
          class:col-xs-2
          widgets
            button
              innerValue:@"<span class=""glyphicon glyphicon-cog""></span>"
              class:btn btn-default sephia-configuration-btn
              onclick

                /*
                 * Opening up "configure settings form", making sure we populate fields with existing settings.
                 */
                clear-widget:cnt
                sys42.utilities.execute-lambda-file:@SEPHIA/configure/configure.hl
                  change:bool:true




/*
 * Creating "datagrid" widget.
 */
create-widget:sephia-content
  parent:cnt
  class:row

  /*
   * This Ajax event is invoked from JavaScript on page, whenever we should for any reasons check POP3 server
   * if it has more emails we need to fetch.
   */
  .onretrieve

    /*
     * This file simply retrieves the next batch of emails from our POP3 server, and stores them into our
     * MySQL database.
     */
    sys42.utilities.execute-lambda-file:@SEPHIA/inbox/helpers/fetch.hl

  oninit

    /*
     * Making sure during initial page load, that we immediately check for new emails.
     */
    p5.web.send-javascript:@"p5.$('sephia-content').raise('.onretrieve');"

  widgets

    /*
     * This is our "inbox datagrid", which displays all the emails currently being viewed by user.
     */
    table
      class:table table-hover table-condensed
      widgets
        thead
          innerValue:@"<tr><th>From</th><th>Subject</th><th>Date</th></tr>"

        /*
         * "Datagrid" content (inbox).
         */
        container:sephia-datagrid
          element:tbody

    /*
     * Wrapper for "reading emails".
     *
     * This widget will contain any emails the user has selected for reading and/or replying to.
     */
    container:sephia-read-emails
      class:sephia-reader





/*
 * Showing inbox.
 */
sys42.utilities.execute-lambda-file:@SEPHIA/inbox/show.hl
