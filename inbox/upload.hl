
/*
 * Invoked by "drag and drop uploader" when a new file has been uploaded to a new/reply email.
 *
 * Creates a unique filename, and saving file to private documents folder, in addition to creating a widget wrapping
 * uploaded file (allowing for deletion), and injecting file wrapping widget into "reply/compose" widget.
 */
p5.types.guid.new
replace:x:/@p5.types.guid.new?value.string
  src:-
save-file:~/documents/private/sephia/attachments/sent-{0}-{1}
  :x:/@replace?value
  :x:/../*/_filename?value
  src:x:/../*/_content?value





/*
 * Creating an "attachment widget" and adding into "reply/compose widget".
 */
p5.web.widgets.find-first-ancestor:x:/../*/_event?value
  _is-reply
p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
  _attachment-wrapper





/*
 * Creating actual widget wrapping the attachment.
 */
create-widget
  parent:x:/@p5.web.widgets.find/*/*?value
  class:sephia-attachment
  _reply-attachment
  _folder:~/documents/private/sephia/attachments/
    :x:/@replace?value
    :x:/../*/_filename?value
  _filename:x:/../*/_filename?value
  _prefix:sent-{0}-
    :x:/@replace?value
  widgets

    /*
     * Displays filename (only) for attachment.
     */
    span
      innerValue:x:/../*/_filename?value

    /*
     * Allows the user to delete the attachment.
     */
    literal
      element:span
      role:button
      class:glyphicon glyphicon-trash sephia-delete-attachment
      onclick

        /*
         * Figuring out filename, and deleting file in its entirety, before deleting "attachment widget".
         */
        p5.web.widgets.get-parent:x:/../*/_event?value
        p5.web.widgets.property.get:x:/@p5.web.widgets.get-parent/*/*?value
          _folder
          _filename
          _prefix
        delete-file:{0}{1}{2}
          :x:/@p5.web.widgets.property.get/*/*/_folder?value
          :x:/@p5.web.widgets.property.get/*/*/_prefix?value
          :x:/@p5.web.widgets.property.get/*/*/_filename?value
        delete-widget:x:/@p5.web.widgets.get-parent/*/*?value
