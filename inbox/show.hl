
/*
 * Opens up the "inbox" of Sephia Five.
 *
 * Notice, the "inbox" contains all email, also sent emails.
 *
 * Optionally pass in [filter] and [offset] as criteria.
 *
 * [filter] will searchj for only emails matching the given [filter] one wy or another (from, content, subject etc)
 * having the search filter.
 *
 * [offset] is an integer number, meaning at which offset to display items from. For instance, if offset is 10, then
 * only the items between 10 and 20 (optionally) matching the given [filter] will be displayed.
 *
 * Default [filter] is empty, and default [offset] is 0.
 */





/*
 * Defaulting offset to 0 if not given.
 */
if:x:/../*/offset?value
  not
  insert-before:x:/../0
    src
      offset:int:0





/*
 * Opening up database connection, making sure we invoke [whoami], to use as criteria for "username" columns
 * in later SQLs.
 */
whoami
p5.io.unroll-path:@SEPHIA/
p5.mysql.connect:[sephia]

  /*
   * Checking if a filter was provided.
   */
  if:x:/../*/filter?value
    and:x:/../*/filter?value
      !=:

      /*
       * A filter was provided.
       *
       * Modifying SQL we're evaluating below, to make sure we only retrieve items matching the specified 
       * filter condition.
       */
    set:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]?value
      src:@"select * from emails 
inner join contacts on emails.sender = contacts.id 
inner join parts on emails.id = parts.emailid
where emails.username = @username and parts.type = 'plain'
and (subject like @filter or contacts.name like @filter or contacts.email like @filter or parts.content like @filter)
order by emails.id desc limit 10 offset @offset"
    add:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]
      src
        @filter:"%{0}%"
          :x:/../*/filter?value

  /*
   * Evaluating actual SQL to retrieve main email records from database.
   */
  p5.mysql.select:@"select * from emails 
inner join contacts on emails.sender = contacts.id 
where emails.username = @username order by emails.id desc limit 10 offset @offset"
    @username:x:/@whoami/*/username?value
    @offset:x:/../*/offset?value

  /*
   * Looping through result set from SQL command.
   */
  for-each:x:/@p5.mysql.select/*

    /*
     * CSS class for HTML table row, which differs is email is read, unread, has attachments, etc.
     */
    _class

    /*
     * Status of email, whether or not it was encrypted, contains attachments and was cryptographically signed, etc.
     */
    _status:

    /*
     * Checking if email has been read.
     *
     * Emails that are already read by user, are rendered with visual clues, indicating they have already been read.
     */
    if:x:/@_dp/#/*/isread?value.int
      =:int:0
      set:x:/@_class?value
        src:{0} sephia-unread
          :x:/@_class?value

    /*
     * Checking if email has been sent by user.
     *
     * Notice, we only have one single "inbox", which contains all emails, both those sent by user himself, and those
     * received from others.
     *
     * Emails are rendered with visual clues though, indicating whether or not they've been sent by user himself, 
     * received from others, etc.
     */
    if:x:/@_dp/#/*/type?value
      =:sent
      set:x:/@_class?value
        src:{0} sephia-sent
          :x:/@_class?value
      add:x:/..for-each/*/create-widget/*/widgets/*/td/[0,1]
        src
          title:This email was sent by you!
    else
      add:x:/..for-each/*/create-widget/*/widgets/*/td/[0,1]
        src
          title:{0} &lt;{1}&gt;
            :x:/@_dp/#/*/name?value
            :x:/@_dp/#/*/email?value

    /*
     * Checking if email has attachments.
     *
     * We display visual clues for emails having attachments.
     */
    p5.mysql.scalar:@"select count(*) from attachments where emailid = @emailid"
      @emailid:x:/@_dp/#/*/id?value
    if:x:/@p5.mysql.scalar?value
      >:long:0
      set:x:/@_status?value
        src:@"{0}<svg class=""sephia-icon""><use xlink:href=""{1}media/si-sprite.svg#si-ant-paper-clip""></use></svg>"
          :x:/@_status?value
          :x:/@p5.io.unroll-path?value

    /*
     * Checking if email is encrypted.
     *
     * We display visual clues for emails that are encrypted.
     */
    if:x:/@_dp/#/*/encrypted?value.int
      =:int:1
      set:x:/@_status?value
        src:@"{0}<svg class=""sephia-icon""><use xlink:href=""{1}media/si-sprite.svg#si-evil-lock""></use></svg>"
          :x:/@_status?value
          :x:/@p5.io.unroll-path?value

    /*
     * Checking if email is signed.
     *
     * We display visual clues for emails that have been cryptographically signed.
     */
    if:x:/@_dp/#/*/signature?value
      set:x:/@_status?value
        src:@"{0}<svg class=""sephia-icon""><use xlink:href=""{1}media/si-sprite.svg#si-evil-pencil""></use></svg>"
          :x:/@_status?value
          :x:/@p5.io.unroll-path?value

    /*
     * Checking if email is already open.
     *
     * Notice, opened emails are rendered in our datagrid with "visual clues" to signal that they're already opened
     * up for viewing.
     *
     * Opened email here, refer to emails that user is already reading, in the "reader" parts of Sephia Five.
     */
    p5.web.widgets.find:sephia-read-emails
      _email-id:x:/@_dp/#/*/id?value
    if:x:/@p5.web.widgets.find/*/*
      set:x:/@_class?value
        src:{0} sephia-open
          :x:/@_class?value

    /*
     * Nicely formatting date.
     */
    p5.types.date.format:x:/@_dp/#/*/date?value
      format:"ddd d. MMM yyyy - HH:mm"

    /*
     * Checking if from name exceeds 19 characters, and if so, right-"trimming" it down to 19.
     */
    _from
    if
      length:x:/@_dp/#/*/name?value
      >:int:19
      split:x:/@_dp/#/*/name?value
        =:int:19
      set:x:/@_from?value
        src:{0} ...
          :x:/@split/0?name
    else
      set:x:/@_from?value
        src:x:/@_dp/#/*/name?value

    /*
     * Checking if subject length exceeds 57 characters, and if so, right-"trimming" it down to 57.
     */
    _subject
    if
      length:x:/@_dp/#/*/subject?value
      >:int:57
      split:x:/@_dp/#/*/subject?value
        =:int:57
      set:x:/@_subject?value
        src:{0} ...
          :x:/@split/0?name
    else
      set:x:/@_subject?value
        src:x:/@_dp/#/*/subject?value

    /*
     * Checking if we have a custom class, and if so, making sure we add it to creation of row.
     */
    if:x:/@_class?value
      trim:x:/@_class?value
      eval-x:x:/+/*/*
      add:x:/..for-each/*/create-widget
        src
          class:x:/@trim?value

    /*
     * Creating table row for email item, and inserting row widget into "table datagrid" for emails.
     */
    create-widget
      parent:sephia-datagrid
      element:tr
      _email-id:x:/@_dp/#/*/id?value
      role:button
      onclick

        /*
         * Open email for reading, by invoking file responsible for doing such, passing in ID of email user wants to read.
         */
        p5.web.widgets.property.get:x:/../*/_event?value
          _email-id
        eval-x:x:/+/*
        sys42.utilities.execute-lambda-file:@SEPHIA/inbox/read.hl
          email-id:x:/@p5.web.widgets.property.get/*/*?value

      widgets

        /*
         * "From" column.
         */
        td
          widgets
            span
              innerValue:x:/@_from?value

        /*
         * "Subject" column.
         */
        td
          innerValue:x:/@_subject?value

        /*
         * "Date" column.
         *
         * Notice, emails are not sorted by dates, since that might create "weird results", if sender's SMTP server
         * is wrongly configured in regards to what time it it.
         *
         * Hence, sometimes, theoretically, an email with a newer date,might render after another email, being "older".
         *
         * This is because emails are rendered in the order they are received, which is more "correct" to use as ordering
         * of emails.
         */
        td
          widgets
            span
              innerValue:x:/@p5.types.date.format?value

        /*
         * Status of email, whether or not it was encrypted, contains attachments, and was cryptographically signed, etc.
         */
        td
          innerValue:x:/@_status?value





/*
 * Updating title of web page, to reflect unread emails.
 */
sys42.utilities.execute-lambda-file:@SEPHIA/inbox/set-title.hl
