
/*
 * Creates the main GUI structure of Sephia Mail.
 */





/*
 * Making sure we modify our main root widget to become a Bootstrap CSS "container".
 */
p5.web.widgets.property.set:cnt
  class:container sephia





/*
 * Creating header and "menu" row.
 */
create-widget:sephia-header
  parent:cnt
  class:row
  events

    /*
     * Event raised when datagrid needs re-databinding.
     *
     * Expects optionally [offset] and [filter], simply passes these two values into evaluation of Hyperlambda file,
     * responsible for re-databinding "inbox".
     */
    sephia.inbox.databind

      /*
       * Invoking file responsible for re-databinding "inbox".
       *
       * First making sure we delete old widgets.
       */
      clear-widget:sephia-datagrid
      add:x:/+
        src:x:/../*(/filter|/offset)
      sys42.utilities.execute-lambda-file:@SEPHIA/show-inbox.hl

  widgets
    div
      class:col-xs-12 sephia-brand
      widgets

        /*
         * Header at the top, feel free to add whatever "branding" widgets you wish here, 
         * replacing the existing widget.
         */
        h1
          innerValue:Sephia Email
    div
      class:sephia-selector
      widgets

        div
          class:col-xs-10
          widgets

            /*
             * This is the "search and page" toolbar at the top.
             *
             * Notice, we wrap it inside the "sephia-selector" CSS class widget, to make it "drop down" during start,
             * and stay in a fixed position during browsing.
             */
            sephia.widgets.search-toolbar
              databind-event:sephia.inbox.databind
              page-size:int:19
              search-accesskey:F


        div
          class:col-xs-2
          widgets
            button
              innerValue:@"<span class=""glyphicon glyphicon-envelope""></span>"
              class:btn btn-success sephia-new-email-btn
              onclick
                sys42.utilities.execute-lambda-file:@SEPHIA/new-email.hl




/*
 * Creating "datagrid" widget.
 */
create-widget:sephia-content
  parent:cnt
  class:row
  .onretrieve
    sys42.utilities.execute-lambda-file:@SEPHIA/fetch-next.hl
  oninit
    p5.web.send-javascript:@"p5.$('sephia-content').raise('.onretrieve');"
  widgets
    table
      class:table table-hover table-condensed
      widgets
        thead
          innerValue:@"<tr><th>From</th><th>Subject</th><th>Date</th></tr>"

        /*
         * "Datagrid" content (inbox).
         */
        container:sephia-datagrid
          element:tbody

    /*
     * Wrapper for "reading emails".
     */
    container:sephia-read-emails
      class:sephia-reader





/*
 * Showing inbox.
 */
sys42.utilities.execute-lambda-file:@SEPHIA/show-inbox.hl
