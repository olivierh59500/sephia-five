
/*
 * Creates the main GUI structure of Sephia Mail.
 */





/*
 * Making sure we modify our main root widget to become a Bootstrap CSS "container".
 */
p5.web.widgets.property.set:cnt
  class:container sephia





/*
 * Creating header and "menu" row.
 */
create-widget:sephia-header
  parent:cnt
  class:row
  widgets
    div
      class:col-xs-12
      widgets
        h1
          innerValue:Sephia Email
    div
      class:sephia-selector
      widgets
        div
          class:input-group
          widgets
            input:sephia-search-textbox
              type:search
              class:form-control
              placeholder:Search ...
              accesskey:F
              onkeydown:"return p5.sephia_search_inbox_onkeydown(event);"
            span
              class:input-group-btn
              widgets
                button:sephia-search-inbox
                  class:btn btn-default
                  innerValue:@"<span class=""glyphicon glyphicon-search""></span>"
                  onclick

                    /*
                     * Re-databinding datagrid, now with "search query".
                     */
                    p5.web.widgets.property.get:sephia-search-textbox
                      value
                    clear-widget:sephia-datagrid
                    eval-x:x:/+/*
                    sys42.utilities.execute-lambda-file:@SEPHIA/show-inbox.hl
                      filter:x:/@p5.web.widgets.property.get/*/*?value
                    p5.web.send-javascript:@"$('#sephia-search-textbox').focus().select();"

                button
                  class:btn btn-default
                  innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
                  onclick

                    /*
                     * Re-databinding datagrid, now without.
                     */
                    clear-widget:sephia-datagrid
                    sys42.utilities.execute-lambda-file:@SEPHIA/show-inbox.hl
                    p5.web.widgets.property.set:sephia-search-textbox
                      value:
                    p5.web.send-javascript:@"$('#sephia-search-textbox').focus().select();"

                button
                  innerValue:@"<span class=""glyphicon glyphicon-envelope""></span>"
                  class:btn btn-default
                  onclick
                    sys42.utilities.execute-lambda-file:@SEPHIA/new-email.hl




/*
 * Creating "datagrid" widget.
 */
create-widget:sephia-content
  parent:cnt
  class:row
  .onretrieve
    sys42.utilities.execute-lambda-file:@SEPHIA/fetch-next.hl
  oninit
    p5.web.send-javascript:@"p5.$('sephia-content').raise('.onretrieve');"
  widgets
    table
      class:table table-hover table-condensed
      widgets
        thead
          innerValue:@"<tr><th>From</th><th>Subject</th><th>Date</th></tr>"

        /*
         * "Datagrid" content (inbox).
         */
        container:sephia-datagrid
          element:tbody

    /*
     * Wrapper for "reading emails".
     */
    container:sephia-read-emails
      class:sephia-reader





/*
 * Showing inbox.
 */
sys42.utilities.execute-lambda-file:@SEPHIA/show-inbox.hl





/*
 * Making surw we trap carriage return, and click "search" button in "search" textbox.
 */
p5.web.include-javascript:@"p5.sephia_search_inbox_onkeydown = function(e) {
  if(e.keyCode == 13) {
    p5.$('sephia-search-inbox').raise('onclick');
    return false;
  }
}"
