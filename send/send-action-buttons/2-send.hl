
/*
 * Send button.
 */
button
  class:float-right button-clear
  innerValue:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-evil-sc-telegram""></use></svg>"
    :x:/@p5.io.unroll-path?value
  title:Send reply
  onclick

    /*
     * Retrieving main "root widget" for reply surface.
     */
    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
      _is-reply

    /*
     * Retrieving data for email, by invoking "lambda event" for widget, that should return
     * whatever data the user has supplied.
     *
     * Notice, this event requires an [_arg] being the ID of the main root "reply/compose" widget.
     * Hence, we retrieve that widget above, and pass it into our event invocation.
     */
    sephia.get-email:x:/@p5.web.widgets.find-first-ancestor/*/*?value

    /*
     * Sanity check.
     */
    if:x:/@sephia.get-email/*/To?value
      =:
      or:x:/@sephia.get-email/*/subject?value
        =:
      or:x:/@sephia.get-email/*/body?value
        =:

      /*
       * Oops, returning early!
       */
      sys42.windows.info-tip:An email needs at least one recipient (To), a Subject and some Content.
        class:info-window info-window-error info-window-longer
      return

    /*
     * Checking if we should do a "force send".
     */
    p5.web.widgets.property.get:x:/../*/_event?value
      _force
    if:x:/@p5.web.widgets.property.get/*/*?value.bool
      add:x:/../*/sys42.utilities.execute-lambda-file
        src
          send-unencrypted:bool:true

    /*
     * Invoking file responsible for sending email, adding all necessary associated fields.
     *
     * Notice, we provide a callback, which will be invoked if the file needs to display a "warning"
     * about that we did not have all recipients' public PGP keys.
     */
    add:x:/+
      src:x:/@sephia.get-email/*
    sys42.utilities.execute-lambda-file:@SEPHIA/send/send-email.hl

    /*
     * Checking if email was successfully sent.
     */
    if:x:/@sys42.utilities.execute-lambda-file?value
      =:bool:true

      /*
       * Success!
       *
       * Notifying user, and re-databinding datagrid, and deleting entire "reply widget".
       */
      sys42.windows.info-tip:Email was successfully sent!
      delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value
      clear-widget:sephia-datagrid
      sys42.utilities.execute-lambda-file:@SEPHIA/inbox/show.hl

    else

      /*
       * Email wasn't sent, because one of the recipients didn't have a public PGP key.
       */
      sys42.windows.info-tip:You did not have encryption keys for all recipients, click the button again, to force sending it anyway.
        class:info-window info-window-warning info-window-longer
      p5.web.widgets.property.set:x:/../*/_event?value
        _force:true
