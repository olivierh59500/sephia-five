
/*
 * Adds a recipient to some "input" type of widget from "contact" table.
 *
 * Expects [widget] being widget where to add result.
 */





/*
 * Forward declaring "return widget" to any who needs it.
 */
eval-x:x:/../**/_return-widget





/*
 * Using a modal window, which allows user to search for contacts.
 */
sys42.windows.modal
  header:Add recipient
  buttons
  widgets
    table
      class:table table-hover table-condensed sephia-recipient-table
      oninit

        /*
         * Doing our initial databind, without any filters.
         */
        sephia.contacts.databind
          offset:0

      events

        /*
         * Custom "lambda events" for modal window.
         */
        sephia.contacts.databind

          /*
           * Cleaning out old records.
           */
          clear-widget:sephia-search-for-recipient

          /*
           * Checking if a [filter] was specified, and if so, modifying our SQL in accordance.
           */
          if:x:/../*/filter?value
            set:x:/../*/p5.mysql.connect/*/p5.mysql.select/[0,1]?value
              src:@"select * from contact where username = @username and (name like @query or email like @query) order by name limit 10 offset {0}"
                :x:/../*/offset?value.int
            add:x:/../*/p5.mysql.connect/*/p5.mysql.select/[0,1]
              src
                @query:%{0}%
                  :x:/../*/filter?value

          /*
           * Connecting to database and selecting 10 top records from "contact" table, sorted by name.
           */
          whoami
          p5.mysql.connect:[sephia]
            p5.mysql.select:@"select * from contact where username = @username order by name limit 10 offset {0}"
              @username:x:/@whoami/*/username?value
              :x:/../*/offset?value.int

            /*
             * Looping through above result set, creating a table row for each result.
             */
            for-each:x:/@p5.mysql.select/*
              create-widget
                element:tr
                parent:sephia-search-for-recipient
                class:sephia-add-recipient
                role:button
                _email:x:/@_dp/#/*/email?value
                onclick

                  /*
                   * Foward declared during creation.
                   */
                  _return-widget:x:/../*/widget?value

                  /*
                   * Adding email from current row into [widget] specified during creation, and closing window.
                   */
                  p5.web.widgets.property.get:x:/../*/_event?value
                    _email

                  /*
                   * Making sure email address is not already in recipient list.
                   */
                  p5.web.widgets.property.get:x:/@_return-widget?value
                    value
                  if
                    fetch:x:/0/0?value
                      index-of:x:/@p5.web.widgets.property.get/*/*?value
                        src:x:/@p5.web.widgets.property.get/@p5.web.widgets.property.get/*/*?value

                    /*
                     * Widget already contains address.
                     */
                    sys42.windows.info-tip:That address is already in your list of recipients.
                      class:info-window info-window-error
                      parent:sys42-windows-modal-body-wrapper
                    return

                  else-if:x:/@p5.web.widgets.property.get/*/*?value

                    /*
                     * Appending new address to widget.
                     */
                    p5.web.widgets.property.set:x:/@_return-widget?value
                      value:{0}, {1}
                        :x:/@p5.web.widgets.property.get/*/*?value
                        :x:/@p5.web.widgets.property.get/@p5.web.widgets.property.get/*/*?value

                  /*
                   * Destroying modal window, setting focus to widget that had address appended into it.
                   */
                  sys42.windows.modal.destroy
                  p5.web.send-javascript:@"$('#{0}').focus().select();"
                    :x:/@_return-widget?value
                  sys42.windows.info-tip:Email was added to your recipient list.

                widgets
                  td
                    innerValue:x:/@_dp/#/*/name?value
                  td
                    innerValue:x:/@_dp/#/*/email?value

      widgets
        tbody:sephia-search-for-recipient
          widgets
    div
      class:input-group col-sm-7 col-sm-offset-5 col-xs-10 col-xs-offset-2
      widgets
        input:sephia-recipients-filter
          type:text
          class:form-control
          placeholder:Filter ...
          oninit

            /*
             * Making sure our textbox gets initial focus.
             */
            sys42.windows.modal.initial-focus:x:/../*/_event?value

          /*
           * Making sure we trap carriage return in textbox.
           */
          onkeydown:"return p5.sephia_recipients_onkeypress(event);"

        span
          class:input-group-btn
          _offset:0
          widgets

            /*
             * Search button.
             */
            button:sephia-recipients-search-button
              class:btn btn-default
              innerValue:@"<span class=""glyphicon glyphicon-search""></span>"
              onclick

                /*
                 * Retrieving specified filter, and re-databinding our "datagrid" again, making sure
                 * "filter textbox" gets focus again.
                 */
                p5.web.widgets.get-parent:x:/../*/_event?value
                p5.web.widgets.property.get:x:/@p5.web.widgets.get-parent/*/*?value
                  _offset
                p5.web.widgets.property.get:sephia-recipients-filter
                  value
                eval-x:x:/+/*
                sephia.contacts.databind
                  filter:x:/@p5.web.widgets.property.get/*/*?value
                  offset:x:/@p5.web.widgets.property.get/@p5.web.widgets.property.get/*/*?value
                p5.web.send-javascript:@"$('#sephia-recipients-filter').focus().select();"

            /*
             * Previous button.
             */
            button
              class:btn btn-default
              innerValue:@"<span class=""glyphicon glyphicon-chevron-left""></span>"
              onclick

                /*
                 * Retrieving current "offset" and verifying it is more than 0.
                 * No need to re-databind datagrid, unless we're actually beoynd first page.
                 */
                p5.web.widgets.get-parent:x:/../*/_event?value
                p5.web.widgets.property.get:x:/@p5.web.widgets.get-parent/*/*?value
                  _offset
                if:x:/@p5.web.widgets.property.get/*/*?value
                  =:0
                  sys42.windows.info-tip:No more records.
                    class:info-window info-window-error
                    parent:sys42-windows-modal-body-wrapper
                  return

                /*
                 * Re-databinding our "datagrid" again, making sure
                 * "filter textbox" gets focus again.
                 */
                set:x:/@p5.web.widgets.property.get/*/*?value
                  -:x:/@p5.web.widgets.property.get/*/*?value.int
                    _:int:10

                /*
                 * Making sure we pass in "filter".
                 */
                p5.web.widgets.property.get:sephia-recipients-filter
                  value
                eval-x:x:/+/*
                sephia.contacts.databind
                  filter:x:/@p5.web.widgets.property.get/*/*?value
                  offset:x:/@p5.web.widgets.property.get/@p5.web.widgets.property.get/*/*?value.int
                p5.web.send-javascript:@"$('#sephia-recipients-filter').focus().select();"

                /*
                 * Updating "offset" (page).
                 */
                p5.web.widgets.property.set:x:/@p5.web.widgets.get-parent/*/*?value
                  _offset:x:/../*/p5.web.widgets.property.get/[0,1]/*/*?value.string

            /*
             * Next button.
             */
            button
              class:btn btn-default
              innerValue:@"<span class=""glyphicon glyphicon-chevron-right""></span>"
              onclick

                /*
                 * Retrieving numbers of rows in datagrid, and verifying it's not less than 10.
                 * No need to re-databind datagrid, unless we might have more records.
                 */
                p5.web.widgets.get-children:sephia-search-for-recipient
                if:x:/@p5.web.widgets.get-children/*/*?count
                  <:int:10
                  sys42.windows.info-tip:No more records.
                    class:info-window info-window-error
                    parent:sys42-windows-modal-body-wrapper
                  return

                /*
                 * Retrieving current "offset" (page).
                 */
                p5.web.widgets.get-parent:x:/../*/_event?value
                p5.web.widgets.property.get:x:/@p5.web.widgets.get-parent/*/*?value
                  _offset

                /*
                 * Re-databinding our "datagrid" again, making sure
                 * "filter textbox" gets focus again.
                 */
                set:x:/@p5.web.widgets.property.get/*/*?value
                  +:x:/@p5.web.widgets.property.get/*/*?value.int
                    _:int:10
                p5.web.widgets.property.get:sephia-recipients-filter
                  value
                eval-x:x:/+/*
                sephia.contacts.databind
                  filter:x:/@p5.web.widgets.property.get/*/*?value
                  offset:x:/@p5.web.widgets.property.get/@p5.web.widgets.property.get/*/*?value.int
                p5.web.send-javascript:@"$('#sephia-recipients-filter').focus().select();"

                /*
                 * Updating "offset" (page).
                 */
                p5.web.widgets.property.set:x:/@p5.web.widgets.get-parent/*/*?value
                  _offset:x:/../*/p5.web.widgets.property.get/[0,1]/*/*?value.string





/*
 * Making surw we trap carriage return, and click "filter" button in "search" textbox.
 */
p5.web.include-javascript:@"p5.sephia_recipients_onkeypress = function(e) {
  if(e.keyCode == 13) {
    p5.$('sephia-recipients-search-button').raise('onclick');
    return false;
  }
}"
