
/*
 * Launcher file for Sephia Five.
 *
 * This file is evaluated (without any parameters) from the CMS when the menu item for the Sephia application
 * is clicked by the user.
 */





/*
 * Verifying user is authorized to access this application.
 */
whoami
if:x:/@whoami/*/default?value
  throw:Sorry, Sephia Five is only accessible for logged in users





/*
 * Including Bootstrap CSS and JS, in addition to custom Sephia Five CSS.
 *
 * Notice, these events will also include jQuery, since Bootstrap (JS) relies on jQuery.
 */
sys42.bootstrap.include-css
sys42.bootstrap.include-javascript
p5.web.include-css-file
  @CMS/media/css/main.css
  @SEPHIA/media/main.css





/*
 * Making sure user has his own private "sephia" folders, for attachments, among other things.
 *
 * Notice, the files in these folders are stored in the user's "private folder", 
 * which means they're not accessible for others, even if they have a direct link to the files.
 */
folder-exists:~/documents/private/sephia/
if:x:/@folder-exists/*?value
  not
  create-folder:~/documents/private/sephia/
folder-exists:~/documents/private/sephia/attachments/
if:x:/@folder-exists/*?value
  not
  create-folder:~/documents/private/sephia/attachments/





/*
 * Making sure we modify our main root widget to become a Bootstrap CSS "container",
 * in addition to adding our own custom CSS root class to the root widget of page, to have a unique CSS selector,
 * to make sure no CSS from other apps are not interfering with our custom CSS.
 *
 * This allows us, at least in theory, to have multiple apps running on the same page, 
 * without interfering with each other.
 */
p5.web.widgets.property.set:cnt
  class:container sephia





/*
 * Making sure user has configured his POP3/SMTP settings, and if not, 
 * opening up configure wizard, and "returning early".
 */
p5.auth.my-settings.get
if:x:/@p5.auth.my-settings.get/*/sephia
  not
  sys42.utilities.execute-lambda-file:@SEPHIA/configure/configure.hl
  return




/*
 * Creating main form.
 */
sys42.utilities.execute-lambda-file:@SEPHIA/create-display.hl
