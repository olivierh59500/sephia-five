
/*
 * Opens up the "inbox" of Sephia Mail.
 *
 * Notice, the "inbox" contains all email, also sent emails.
 *
 * Optionally pass in [filter] as search criteria.
 */





/*
 * Showing top 19 email in "datagrid.
 */
whoami
p5.mysql.connect:[sephia]

  /*
   * Checking if a filter was provided.
   */
  if:x:/../*/filter?value
    and:x:/../*/filter?value
      !=:

      /*
       * A filter was provided.
       *
       * Modifying SQL we're evaluating below.
       */
    set:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]?value
      src:@"select * from email 
inner join contact on email.sender = contact.id 
inner join part on email.id = part.emailid
where email.username = @username and part.type = 'plain'
and (subject like @filter or contact.name like @filter or contact.email like @filter or part.content like @filter)
order by email.id desc limit 19"
    add:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]
      src
        @filter:"%{0}%"
          :x:/../*/filter?value

  /*
   * Evaluating actual SQL.
   */
  p5.mysql.select:@"select * from email 
inner join contact on email.sender = contact.id 
where email.username = @username order by email.id desc limit 19"
    @username:x:/@whoami/*/username?value





  /*
   * Looping through result set from SQL command.
   */
  for-each:x:/@p5.mysql.select/*

    /*
     * CSS class for HTML table row, which differs is email is read, unread, has attachments, etc.
     */
    _class:sephia-email-row

    /*
     * Checking if email has been read.
     */
    if:x:/@_dp/#/*/isread?value.int
      =:int:0
      set:x:/@_class?value
        src:{0} sephia-unread
          :x:/@_class?value

    /*
     * Checking if email has been sent by user.
     */
    if:x:/@_dp/#/*/type?value
      =:sent
      set:x:/@_class?value
        src:{0} sephia-outbox
          :x:/@_class?value
      add:x:/..for-each/*/create-widget
        src
          title:This email was sent by you!
    else
      add:x:/..for-each/*/create-widget
        src
          title:{0} &lt;{1}&gt;
            :x:/@_dp/#/*/name?value
            :x:/@_dp/#/*/email?value

    /*
     * Checking if email has attachments.
     */
    p5.mysql.scalar:@"select count(*) from attachments where emailid = @emailid"
      @emailid:x:/@_dp/#/*/id?value
    if:x:/@p5.mysql.scalar?value
      >:long:0
      set:x:/@_class?value
        src:{0} sephia-has-attachment
          :x:/@_class?value

    /*
     * Checking if email is encrypted.
     */
    if:x:/@_dp/#/*/encrypted?value.int
      =:int:1
      set:x:/@_class?value
        src:{0} sephia-encrypted
          :x:/@_class?value

    /*
     * Checking if email is signed.
     */
    if:x:/@_dp/#/*/signature?value
      set:x:/@_class?value
        src:{0} sephia-signed
          :x:/@_class?value

    /*
     * Checking if email is already open.
     */
    p5.web.widgets.find:sephia-read-emails
      _email-id:x:/@_dp/#/*/id?value
    if:x:/@p5.web.widgets.find/*/*
      set:x:/@_class?value
        src:{0} sephia-open
          :x:/@_class?value

    /*
     * Nicely formatting date.
     */
    p5.types.date.format:x:/@_dp/#/*/date?value

    /*
     * Creating table row for email item.
     */
    create-widget
      parent:sephia-datagrid
      element:tr
      class:x:/@_class?value
      _email-id:x:/@_dp/#/*/id?value
      role:button
      onclick

        /*
         * Open email for reading, by invoking file responsible for doing such, passing in ID of email.
         */
        p5.web.widgets.property.get:x:/../*/_event?value
          _email-id
        eval-x:x:/+/*
        sys42.utilities.execute-lambda-file:@SEPHIA/open-email.hl
          email-id:x:/@p5.web.widgets.property.get/*/*?value

      widgets
        td
          widgets
            span
              innerValue:x:/@_dp/#/*/name?value
        td
          innerValue:x:/@_dp/#/*/subject?value
        td
          widgets
            span
              innerValue:x:/@p5.types.date.format?value





  /*
   * Updating title of web page, to reflect unread emails.
   */
  p5.mysql.scalar:@"select count(*) from email where isread=0 and username = @username"
    @username:x:/@whoami/*/username?value
  if:x:/@p5.mysql.scalar?value.int
    =:int:0
    p5.web.page.set-title:@"Sephia"
  else
    p5.web.page.set-title:@"Sephia ({0})"
      :x:/@p5.mysql.scalar?value
