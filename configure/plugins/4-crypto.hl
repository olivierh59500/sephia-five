
/*
 * Cryptography settings.
 */
Crypto:sephia-crypto-settings
  widgets

    /*
     * Cryptography settings goes here.
     */
    label
      for:sephia-settings-gpg-fingerprint
      innerValue:PGP key fingerprint
    input:sephia-settings-gpg-fingerprint
      type:text
      placeholder:Fingerprint will show up here ...
      value:
      oninit

        /*
         * Making sure we hide "create new key" by default.
         */
        p5.web.widgets.property.set:sephia-settings-create-new-pgp-key-wrapper
          visible:false

        /*
         * Checking if fingerprint settings and widget is empty, and if so, we populate the fingerprint widget with the first available fingerprint
         * from GnuPG matching the user's email address.
         */
        p5.web.widgets.property.get:x:/../*/_event?value
          value
        p5.auth.my-settings.get:sephia
        if:x:/@p5.web.widgets.property.get/*/*?value
          =:
          and:x:/@p5.auth.my-settings.get/*/sephia?count
            =:int:0
            or:x:/@p5.auth.my-settings.get/*/sephia/*/fingerprint?value
              not
            or:x:/@p5.auth.my-settings.get/*/sephia/*/fingerprint?value
              =:

          /*
           * Fingerprint is empty, therefor we lookup into GnuPG to see if there's an existing keypair for user's email address, if
           * user has supplied an email address that is.
           */
          p5.web.widgets.property.get:sephia-settings-email
            value
          if:x:/@p5.web.widgets.property.get/*/*?value
            =:
            return

          /*
           * User has supplied an email address, but not a fingerprint, checking towards GnuPG database to see if there exists
           * a keypair for user's email address.
           */
          p5.crypto.list-private-keys:x:/../*/p5.web.widgets.property.get/*/*?value
          if:x:/@p5.crypto.list-private-keys/*?count
            >:int:0

            /*
             * There already is a keypair with the supplied email address, using that email address.
             */
            p5.web.widgets.property.set:sephia-settings-gpg-fingerprint
              value:x:/@p5.crypto.list-private-keys/0/*/fingerprint?value

        /*
         * Checking if there's an accosiated key with user, somehow (due to having used previously created GnuPG key)
         */
        p5.web.widgets.property.get:x:/../*/_event?value
          value
        if:x:/@p5.web.widgets.property.get/*/*?value
          !=:

          /*
           * Making sure we allow user to submit public key to keyserver.
           */
          p5.web.widgets.property.set:sephia-settings-submit-key
            visible:true
            

    label:sephia-settings-label-gpg-password
      for:sephia-settings-gpg-password
      innerValue:GnuPG Password
    input:sephia-settings-gpg-password
      type:password
      placeholder:Your GnuPG Password ...
      autocomplete:new-password
      title:Your GnuPG password
      value:
    button
      innerValue:Create new PGP keypar
      style:"margin-right:15px;"
      onclick

        /*
         * Verifying user has filled out name and email from first tab, before allow him to create a key.
         */
        p5.web.widgets.property.get:sephia-settings-name
          value
        p5.web.widgets.property.get:sephia-settings-email
          value
        if:x:/../*/p5.web.widgets.property.get/[0,1]/*/*?value
          =:
          or:x:/../*/p5.web.widgets.property.get/[1,2]/*/*?value
            =:
          sys42.windows.info-tip:You have to fill out your name and email in the first section first.
            class:info-window info-window-error info-window-longer
          return

        /*
         * Setting create new key to visible.
         */
        p5.web.widgets.property.set:sephia-settings-create-new-pgp-key-wrapper
          visible:true

    button:sephia-settings-submit-key
      innerValue:Submit public key to Ubuntu key server
      style:"margin-right:15px;"
      visible:false
      title:Will submit your public key to the Ubuntu key server.
      onclick

        /*
         * Retrieving key and URL encoding it, to prepare it for HTTP POST towards Ubuntu key server.
         */
        p5.web.widgets.property.get:sephia-settings-gpg-fingerprint
          value
        p5.crypto.get-public-key:x:/@p5.web.widgets.property.get/*/*?value
        p5.html.url-encode:x:/@p5.crypto.get-public-key?value

        /*
         * HTTP POSTing key to Ubuntu key server
         */
        p5.http.post:"http://keyserver.ubuntu.com/pks/add"
          Content-Type:application/x-www-form-urlencoded
          content:"keytext={0}"
            :x:/@p5.html.url-encode?value

        /*
         * Checking status, and giving user feedback about operation.
         */
        if:x:/@p5.http.post/*/result/*/status?value
          !=:OK

          /*
           * Oops, error!
           */
          sys42.windows.info-tip:@"Something went wrong, mesage from server was; '{0}'"
            :x:/@p5.http.post/*/result/*/Status-Description?value
            class:info-window info-window-error info-window-even-longer

        else

          /*
           * Success!
           */
          sys42.windows.info-tip:Key was succesfully submitted to key server!

    button:sephia-settings-attach-key-to-signature
      innerValue:Attach this public key to your signature
      style:"margin-right:15px;"
      title:Will append this PGP public key to your email's signature
      oninit

        /*
         * Checking if signature already contains PGP key, and if so, making this button invisible.
         */
        p5.web.widgets.property.get:sephia-settings-signature
          value
        if
          fetch:x:/0/0?value
            index-of:x:/@p5.web.widgets.property.get/*/*?value
              src:"-----BEGIN PGP PUBLIC KEY BLOCK-----"
          p5.web.widgets.property.set:x:/../*/_event?value
            visible:false
        else
          p5.web.widgets.property.set:x:/../*/_event?value
            visible:true

      onclick

        /*
         * Modifying the signature textarea value, appending PGP public key, unless it's already there from before.
         */
        p5.web.widgets.property.get:sephia-settings-gpg-fingerprint
          value
        p5.crypto.get-public-key:x:/@p5.web.widgets.property.get/*/*?value
        p5.web.widgets.property.get:sephia-settings-signature
          value
        p5.web.widgets.property.set:sephia-settings-signature
          value:@"{0}

Fingerprint; {1}

{2}"
            :x:/@p5.web.widgets.property.get/*/*?value
            :x:/../*/p5.web.widgets.property.get/[0,1]/*/*?value
            :x:/@p5.crypto.get-public-key?value
        sys42.windows.info-tip:PGP public key was appended into your signature.
        p5.web.widgets.property.set:x:/../*/_event?value
          visible:false

    /*
     * Wrapper for creating a new PGP key.
     */
    container:sephia-settings-create-new-pgp-key-wrapper
      widgets
        h3:sephia-create-pgp-key-header
          innerValue:Create new PGP keypair
          style:"margin-top:30px;"
        p:sephia-create-pgp-key-body
          innerValue:Will create a new PGP keypair, and install into your GnuPG database, and use this as your PGP keypair for Sephia.
        label:sephia-settings-label-pgp-strength
          for:sephia-settings-gpg-password-strength
          innerValue:Strength of PGP Key
        select:sephia-settings-pgp-strength
          class:sephia-select
          widgets
            option
              innerValue:1024
            option
              innerValue:2048
            option
              innerValue:4096
              selected
        label:sephia-settings-label-gpg-password-create
          for:sephia-settings-gpg-password-repeat
          innerValue:GnuPG password
        input:sephia-settings-gpg-password-create
          type:password
          placeholder:GnuPG password ...
          autocomplete:new-password
          title:Your GnuPG password
          value:
        label:sephia-settings-label-gpg-password-repeat
          for:sephia-settings-gpg-password-repeat
          innerValue:Repeat password
        input:sephia-settings-gpg-password-repeat
          type:password
          placeholder:Repeat password ...
          autocomplete:new-password
          title:Your GnuPG password
          value:

        button:sephia-settings-create-new-key
          innerValue:Create keypair
          onclick

            /*
             * Creating new PGP keypair with name/email and password, making sure passwords above are the same, and that
             * name and email is already filled out.
             */
            _widgets
              sephia-settings-name
              sephia-settings-email
              sephia-settings-gpg-password-create
              sephia-settings-gpg-password-repeat
              sephia-settings-pgp-strength
            p5.web.widgets.property.get:x:/@_widgets/*?name
              value

            /*
             * Verifying password was supplied.
             */
            trim:x:/@p5.web.widgets.property.get/*/sephia-settings-gpg-password-create/*?value
            if:x:/@trim?value
              =:
              sys42.windows.info-tip:No GnuPG password supplied
                class:info-window info-windows-error
              return

            /*
             * Verifying passwords are matching each other.
             */
            if:x:/@p5.web.widgets.property.get/*/sephia-settings-gpg-password-create/*?value
              !=:x:/@p5.web.widgets.property.get/*/sephia-settings-gpg-password-repeat/*?value

              /*
               * Warning user, and returning early, to abort operation.
               */
              sys42.windows.info-tip:Passwords doesn't match each other!
                class:info-window info-window-error
              return

            /*
             * Verifying name and emails has been filled out.
             */
            if:x:/@p5.web.widgets.property.get/*/sephia-settings-name/*?value
              =:
              or:x:/@p5.web.widgets.property.get/*/sephia-settings-email/*?value
                =:
              sys42.windows.info-tip:@"Please fill out name and email in the ""Miscellaneous"" section first!"
                class:info-window info-window-error
              return

            /*
             * Checking if we have warned user that operation might take a VERY, VERY long time, and if not, warning user, and returning early.
             */
            p5.web.widgets.property.get:x:/../*/_event?value
              _has-warned
            if:x:/@p5.web.widgets.property.get/*/*?value
              not
              p5.web.widgets.property.set:x:/../*/_event?value
                _has-warned:yup!
                innerValue:Click me again to start creating key!
              sys42.windows.info-tip:@"This operation might take a VERY long time, depending upon the size of your key. 
Please just let your computer finish the process, without leaving this page. When it is done, the fingerprint of your new PGP key will be populated
with your key's value. Please <strong>click button again</strong> to start process of creating your key!"
                class:info-window info-window-warning info-window-even-longer
              return

            /*
             * Creating PGP key based upon data retrieved from widgets.
             */
            p5.crypto.create-pgp-keypair
              identity:{0} <{1}>
                :x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-name/*?value
                :x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-email/*?value
              password:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-gpg-password-create/*?value
              strength:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-pgp-strength/*?value

            /*
             * Hiding all widgets that user should not be able to interact with anymore!
             */
            p5.web.widgets.property.set:sephia-settings-gpg-fingerprint
              value:x:/@p5.crypto.list-private-keys/0/*/fingerprint?value
              disabled
            p5.web.widgets.property.set:sephia-settings-create-new-pgp-key-wrapper
              visible:false
            p5.web.widgets.property.set:sephia-settings-gpg-password
              value:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-gpg-password-create/*?value

            /*
             * Making sure we allow user to submit public key to keyserver.
             */
            p5.web.widgets.property.set:sephia-settings-submit-key
              visible:true

            /*
             * Feedback to user.
             */
            p5.web.widgets.property.set:sephia-settings-gpg-fingerprint
              value:x:/@p5.crypto.create-pgp-keypair/*/fingerprint?value
            sys42.windows.info-tip:PGP keypair was successfully created

        button
          innerValue:Cancel
          style:"margin-left:15px;"
          onclick

            /*
             * Setting create new key to visible.
             */
            p5.web.widgets.property.set:sephia-settings-create-new-pgp-key-wrapper
              visible:false
