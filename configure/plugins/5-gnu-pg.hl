
/*
 * Advanced settings.
 */
GnuPG:sephia-gnupg-settings
  widgets

    /*
     * Allows user to search for cryptography keys in local GnuPG database.
     */
    sys42.widgets.uploader
      filter:asc
      allow-multiple:false
      class:sephia-uploader
      clickable:false
      .onupload

        /*
         * Invoking event responsible for importing key, passing in [_content].
         */
        gaiasoul.sephia.import-keyring:x:/@_content?value

    label
      for:sephia-settings-search-for-keys-locally
      innerValue:Search GnuPG for keypairs
      style:"z-index:1;position:relative;"
      oninit

        /*
         * Warning user.
         */
        sys42.windows.info-tip:Please be careful in here!!
          class:info-window info-window-warning info-window-longer

    input:sephia-settings-search-for-keys-locally
      type:search
      placeholder:Search for a PGP key here ...
      style:"width:75%;z-index:1;position:relative;"
      onkeypress:"return p5.sephia_search_for_keys_locally_onkeypress(event);"
      oninit

        /*
         * Setting initial focus to search textbox.
         */
        p5.web.send-javascript:@"p5.$('sephia-settings-search-for-keys-locally').el.focus();p5.$('sephia-settings-search-for-keys-locally').el.select();"

        /*
         * Making sure we correctly trap carriage return.
         */
        p5.web.include-javascript:@"p5.sephia_search_for_keys_locally_onkeypress = function(e) {
  if(e.keyCode == 13) {
    p5.$('sephia-settings-search-for-keys-locally-button').raise('onclick');
    return false;
  }
}"

    button:sephia-settings-search-for-keys-locally-button
      innerValue:Search
      style:"width:20%;margin-left:5%;z-index:1;position:relative;"
      onclick

        /*
         * Searching for key in GnuPG database.
         */
        p5.web.widgets.property.get:sephia-settings-search-for-keys-locally
          value
        p5.crypto.list-public-keys:x:/@p5.web.widgets.property.get/*/*?value

        /*
         * Setting focus back to search textbox.
         */
        p5.web.send-javascript:@"p5.$('sephia-settings-search-for-keys-locally').el.focus();p5.$('sephia-settings-search-for-keys-locally').el.select();"

        /*
         * Applying results of above operation to "datagrid".
         */
        clear-widget:sephia-settings-gnupg-search-results

        /*
         * Verifying there was any results.
         */
        if:x:/@p5.crypto.list-public-keys/*?count
          =:int:0
          sys42.windows.info-tip:No results
          return

        .get-key
          p5.crypto.get-key-details:x:/../*/_dn/#?name
          p5.crypto.list-private-keys:x:/../*/_dn/#?name
          if:x:/@p5.crypto.list-private-keys/*
            set:x:/../*/return/*/td/[0,1]/*?value
              src:pub/sec
          else
            set:x:/../*/return/*/td/[0,1]/*?value
              src:pub
          p5.types.date.format:x:/@p5.crypto.get-key-details/*/*/creation-time?value
            format:"ddd d. MMM yyyy - HH:mm"
          p5.html.html-encode:x:/@p5.crypto.get-key-details/*/*/user-ids/*?value
          eval-x:x:/+/*/*|/+/**/_fingerprint
          return
            td
              innerValue
            td
              innerValue:x:/@p5.crypto.get-key-details/*/*/strength?value
            td
              innerValue:x:/@p5.crypto.get-key-details/*?name
            td
              innerValue:x:/@p5.html.html-encode?value
            td
              innerValue:x:/@p5.types.date.format?value
            td
              widgets
                button
                  innerValue:Delete
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    _fingerprint:x:/@p5.crypto.get-key-details/*?name

                    /*
                     * Deleting key
                     */
                    p5.crypto.delete-private-key:x:/@_fingerprint?value
                    p5.crypto.delete-public-key:x:/@_fingerprint?value
                    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                      element:tr
                    delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                    sys42.windows.info-tip:Key was deleted!

                    /*
                     * Checking if key was user's key, and if so, deleting widget's value.
                     */
                    p5.web.widgets.property.get:sephia-settings-gpg-fingerprint
                      value
                    if:x:/@p5.web.widgets.property.get/*/*?value
                      =:x:/@_fingerprint?value
                      p5.web.widgets.property.set:sephia-settings-gpg-fingerprint
                        value:
                      p5.web.widgets.property.set:sephia-settings-gpg-password
                        value:
            td
              widgets
                button
                  innerValue:Export
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    _fingerprint:x:/@p5.crypto.get-key-details/*?name

                    /*
                     * Retrieving key, and downloading it to user's client.
                     */
                    p5.crypto.get-public-key:x:/@_fingerprint?value
                    p5.crypto.get-private-key:x:/@_fingerprint?value
                    set:x:/+?value
                      src:{0}{1}
                        :x:/@p5.crypto.get-public-key/*?value
                        :x:/@p5.crypto.get-private-key/*?value
                    _content
                    sys42.cms.download-content:x:/@_content?value
                      filename:keyring.asc
                      _file-type:text/plain

                    /*
                     * Displaying feedback to user.
                     */
                    sys42.windows.info-tip:Please store file in a secure location.

        apply:x:/../*/create-widget/*/widgets/*/table/*/widgets
          src:x:/@p5.crypto.list-public-keys/*
          template
            tr
              widgets
                {@eval}:x:/@.get-key

        /*
         * Creating datagrid to display results.
         */
        create-widget
          parent:sephia-settings-gnupg-search-results
          widgets
            table
              class:sephia-smaller-table
              widgets
                tr
                  innerValue:"<th>Type</th><th>Size</th><th>Fingerprint</th><th>Identity</th><th>Created</th><th>Delete</th><th>Export</th>"

        /*
         * Warning user about deletion of private keys!
         */
        sys42.windows.info-tip:Warning, if you delete a private key, you won't be able to read any future emails encrypted for it!
          class:info-window info-window-warning info-window-longer

    container:sephia-settings-gnupg-search-results
      style:"position:relative;z-index:1;"

    /*
     * Allows user to search for cryptography keys
     */
    label
      for:sephia-settings-search-for-keys
      innerValue:Search on Ubuntu keyserver for public key
      style:"z-index:1;position:relative;margin-top:25px;"
    input:sephia-settings-search-for-keys
      type:search
      placeholder:Search for a PGP key here ...
      style:"width:75%;z-index:1;position:relative;"
      onkeypress:"return p5.sephia_search_for_keys_externally_onkeypress(event);"
      oninit

        /*
         * Making sure we correctly trap carriage return.
         */
        p5.web.include-javascript:@"p5.sephia_search_for_keys_externally_onkeypress = function(e) {
  if(e.keyCode == 13) {
    p5.$('sephia-settings-search-for-keys-externally-button').raise('onclick');
    return false;
  }
}"

    button:sephia-settings-search-for-keys-externally-button
      innerValue:Search
      style:"width:20%;margin-left:5%;z-index:1;position:relative;"
      onclick

        /*
         * Searching for key on Ubuntu key server.
         */
        p5.web.widgets.property.get:sephia-settings-search-for-keys
          value

        /*
         * Sanity check.
         */
        if:x:/@p5.web.widgets.property.get/*/*?value
          =:
          sys42.windows.info-tip:Please supply a search query
            class:info-window info-window-error
          return

        /*
         * URL encoding, and retrieving result from Ubuntu key server.
         */
        p5.html.url-encode:x:/@p5.web.widgets.property.get/*/*?value
        p5.http.get:"https://keyserver.ubuntu.com/pks/lookup?op=vindex&search={0}&fingerprint=on"
          :x:/@p5.html.url-encode?value

        /*
         * Sanity checking results.
         */
        if:x:/@p5.http.get/*/result/*/status?value
          !=:OK
          sys42.windows.info-tip:Something went wrong when searching for key, message from server was '{0}'
            :x:/@p5.http.get/*/result/*/Status-Description?value
            class:info-window info-window-error info-window-even-longer
          return

        /*
         * Parsing results.
         */
        _encode
          p5.html.html-encode:x:/../*/_dn/#/*/span/[0,1]/*/#text?value
          eval-x:x:/+/*
          return
            innerValue:x:/@p5.html.html-encode?value
        _strip1
          trim:x:/../*/_dn/#/*/#text/[0,1]?value
            chars:" /"
          eval-x:x:/+/*
          return
            innerValue:x:/@trim?value
        _strip2
          split:x:/../*/_dn/#/*/#text/[1,2]?value
            =:"Fingerprint="
          eval-x:x:/+/*
          return
            innerValue:x:/@split/1?name
        _strip3
          split:x:/../*/_dn/#/*/#text/[1,2]?value
            =:"Fingerprint="
          trim:x:/@split/0?name
          eval-x:x:/+/*
          return
            innerValue:x:/@trim?value
        clear-widget:sephia-key-search-results
        p5.html.html2lambda:x:/@p5.http.get/*/result/*/content?value
        apply:x:/../*/create-widget/*/widgets/*/table/*/widgets
          src:x:/@p5.html.html2lambda/*/html/*/body/*/pre/[1,](!/*/span/*/\@class/=warn/./.)
          template
            tr
              role:button
              onclick
                {_url}:x:/*/a/[0,1]/*/\@href?value
                gaiasoul.sephia.import-public-ubuntu-key:{0}{1}
                  :"https://keyserver.ubuntu.com"
                  :x:/@_url?value
              widgets
                td
                  {@eval}:x:/@_strip1
                td
                  {@eval}:x:/@_strip2
                td
                  {@eval}:x:/@_encode
                td
                  {@eval}:x:/@_strip3

        /*
         * Creating widget with result.
         */
        create-widget:sephia-key-search-results-table
          parent:sephia-key-search-results
          widgets
            table
              class:sephia-smaller-table sephia-key-search-results
              widgets
                tr
                  innerValue:"<th>Size</th><th>Fingerprint</th><th>Identity</th><th>Created</th>"
            p
              innerValue:Click row you wish to import into your local GnuPG database

        /*
         * Setting focus back to search textbox.
         */
        p5.web.send-javascript:@"p5.$('sephia-settings-search-for-keys').el.focus();p5.$('sephia-settings-search-for-keys').el.select();"

    /*
     * Search results.
     */
    container:sephia-key-search-results
      style:"position:relative;z-index:1;"
      events

        /*
         * Imports the public key from the given [_arg] Ubuntu URL.
         */
        gaiasoul.sephia.import-public-ubuntu-key

          /*
           * Downloading KEY using HTTP GET request.
           */
          p5.http.get:x:/../*/_arg?value
          p5.html.html2lambda:x:/@p5.http.get/*/result/*/content?value
          p5.crypto.import-public-pgp-key:x:/@p5.html.html2lambda/*/html/*/body/*/pre/[0,1]/*/#text?value
          sys42.windows.info-tip:Public PGP cryptography key was successfully imported.

        /*
         * Imports the given [_arg] keyring
         */
        gaiasoul.sephia.import-keyring

          /*
           * Making sure we can trap exceptions.
           */
          try

            /*
             * Importing all public and private keys in key ring, by invoking Active Event responsible for importing keyring
             * from textual content.
             */
            gaiasoul.sephia.pgp.import.public-keyring-keys:x:/../*/_arg?value
            gaiasoul.sephia.pgp.import.private-keyring-keys:x:/../*/_arg?value

            /*
             * Showing feedback to user.
             */
            sys42.windows.info-tip:{0} public key(s) and {1} private key(s) were successfully imported
              :x:/..try/**/gaiasoul.sephia.pgp.import.public-keyring-keys/*?count
              :x:/..try/**/gaiasoul.sephia.pgp.import.private-keyring-keys/*?count
              class:info-window info-window-longer

          catch

            /*
             * Oops ...!!
             */
            sys42.windows.info-tip:Something went wrong, message from system was '{0}'
              :x:/@message?value
              class:info-window info-window-error info-window-even-longer

    /*
     * Allows user to import an armored PGP key directly as text.
     */
    label
      for:sephia-settings-pgp-import-text
      innerValue:Import PGP key as text
      style:"z-index:1;position:relative;margin-top:25px;"
    literal:sephia-settings-pgp-import-text
      element:textarea
      rows:7
      style:"position:relative;z-index:1;"
      placeholder:Paste PGP key directly into here to import it into your GnuPG database ...
    button
      style:"position:relative;z-index:1;margin-bottom:50px;"
      innerValue:Import
      onclick

        /*
         * Retrieves textarea content, and imports key(s).
         */
        p5.web.widgets.property.get:sephia-settings-pgp-import-text
          value
        gaiasoul.sephia.import-keyring:x:/@p5.web.widgets.property.get/*/*?value

