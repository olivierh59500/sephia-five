
/*
 * Allows the user to configure Sephia Mail.
 */





/*
 * Informing user that he must configure his settings before he can proceed.
 */
sys42.windows.info-tip:You must configure Sephia Mail before you can start using it!





/*
 * Loading files from "configure plugins" folder, and creates a tabview for each of them.
 */
list-files:@SEPHIA/configure/plugins/
  filter:.hl
load-file:x:/-/*?name
add:x:/../*/create-widget/**/items
  src:x:/@load-file/*/*





/*
 * Creating configure widget.
 */
create-widget:sephia-configure
  parent:cnt
  class:row prepend-top
  widgets
    container
      class:col-xs-12
      widgets
        sys42.widgets.tab
          items
    container
      class:col-xs-12 text-right
      widgets
        button
          class:btn btn-default
          innerValue:Done
          onclick

            /*
             * Easy version ...
             */
            p5.web.reload-location




return
/*
 * Showing a wizard window, asking user for credentials.
 */
sys42.windows.wizard
  header:Please configure inbox
  body:<p>Supply settings for your POP3/SMTP account(s).</p>
  data
    POP:pop.gmail.com
    PORT:995
    Username:YOUR-GMAIL-USERNAME@gmail.com
    Password:YOUR-GMAIL-PASSWORD
    SSL:Yes
      _options
        No:No
        Yes:Yes

    SMTP:smtp.gmail.com
    PORT:465
    SSL:Yes
      _options
        No:No
        Yes:Yes
    Username:YOUR-GMAIL-USERNAME@gmail.com
    Password:YOUR-GMAIL-PASSWORD

    Name:John Doe
    Email:YOUR-GMAIL-USERNAME@gmail.com
    GPG Pwd:YOUR-Gnu-Privacy-Guard-PASSWORD
    Signature:"Support your local WikiLeaks chapter - https://wikileaks.org"

  .onok
    sys42.windows.wizard.get-values

    /*
     * Verifying username/password is supplied for both POP3 and SMTP
     */
    if:x:/@sys42.windows.wizard.get-values/*/Username/[0,1]?value
      =:
      or:x:/@sys42.windows.wizard.get-values/*/Password/[0,1]?value
        =:
      or:x:/@sys42.windows.wizard.get-values/*/Username/[1,2]?value
        =:
      or:x:/@sys42.windows.wizard.get-values/*/Password/[1,2]?value
        =:
      or:x:/@sys42.windows.wizard.get-values/*/Name?value
        =:
      or:x:/@sys42.windows.wizard.get-values/*/Email?value
        =:
      or:x:/@sys42.windows.wizard.get-values/*/PGP Pwd?value
        =:
      sys42.windows.info-tip:You have to supply username and password for both SMTP and POP3 settings, in addition to email and GPG password.
        class:info-window info-window-longer info-window-error
        parent:sys42-windows-modal-body-wrapper
      return:bool:false

    /*
     * Data is valid (somehow).
     */
    eval-x:x:/+/*/*
    p5.auth.my-settings.set
      sephia
        pop3-server:x:/@sys42.windows.wizard.get-values/*/POP?value
        pop3-port:x:/@sys42.windows.wizard.get-values/*/PORT/[0,1]?value
        pop3-use-ssl:x:/@sys42.windows.wizard.get-values/*/SSL/[0,1]?value
        pop3-username:x:/@sys42.windows.wizard.get-values/*/Username/[0,1]?value
        pop3-password:x:/@sys42.windows.wizard.get-values/*/Password/[0,1]?value
        smtp-server:x:/@sys42.windows.wizard.get-values/*/SMTP?value
        smtp-port:x:/@sys42.windows.wizard.get-values/*/PORT/[1,2]?value
        smtp-use-ssl:x:/@sys42.windows.wizard.get-values/*/SSL/[1,2]?value
        smtp-username:x:/@sys42.windows.wizard.get-values/*/Username/[1,2]?value
        smtp-password:x:/@sys42.windows.wizard.get-values/*/Password/[1,2]?value
        name:x:/@sys42.windows.wizard.get-values/*/Name?value
        email:x:/@sys42.windows.wizard.get-values/*/Email?value
        gpg-password:x:/@sys42.windows.wizard.get-values/*/GPG Pwd?value
        signature:x:/@sys42.windows.wizard.get-values/*/Signature?value

    /*
     * Returning success, to close modal window.
     * Which doesn't really matter, since we're doing a reload of page anyways ...
     */
    p5.web.reload-location
    return:bool:true
