
/*
 * Allows the user to configure Sephia Five.
 */





/*
 * Loading files from "configure plugins" folder, and creates a tabview for each of them.
 */
list-files:@SEPHIA/configure/plugins/
  filter:.hl
load-file:x:/-/*?name
add:x:/../*/create-widget/**/items
  src:x:/@load-file/*/*





/*
 * Creating configure widget.
 *
 * Forward evaluating argument first.
 */
eval-x:x:/+/**/_is-change
create-widget:sephia-configure
  parent:cnt
  class:container
  widgets
    div
      class:row sephia-brand
      widgets
        a
          href:"https://gaiasoul.com"
          target:_blank
          class:column
          widgets
            h1
              innerValue:Configuration
    container
      class:row
      widgets
        div
          class:column
          widgets

            /*
             * This one will by dynamically populated according to the files you've got in "/plugins/".
             */
            sys42.widgets.tab
              items

    container
      class:row
      widgets
        div
          class:column
          widgets

            /*
             * Close (without saving) button.
             */
            button:sephia-settings-close-button
              innerValue:Close
              title:Close without saving
              class:float-right button-clear
              onclick

                /*
                 * Easy version ...
                 * Simply reloading page, to re-initialize app in its entirety.
                 */
                p5.web.reload-location

            /*
             * Close (and save) button.
             */
            button
              innerValue:Save
              title:Save and close
              class:float-right button-clear
              onclick

                /*
                 * Forward evaluated above, if true, doesn't require updates to any passwords, unless passwords
                 * have been explicitly changed by user.
                 */
                _is-change:x:/../*/change?value

                /*
                 * Retrieving settings from form.
                 */
                _textboxes
                  sephia-settings-name
                  sephia-settings-email
                  sephia-settings-gpg-password
                  sephia-settings-gpg-password-repeat
                  sephia-settings-signature
                  sephia-settings-pop3-server
                  sephia-settings-pop3-port
                  sephia-settings-pop3-username
                  sephia-settings-pop3-password
                  sephia-settings-smtp-server
                  sephia-settings-smtp-port
                  sephia-settings-smtp-username
                  sephia-settings-smtp-password
                  sephia-settings-gpg-fingerprint
                _checkboxes
                  sephia-settings-pop3-ssl
                  sephia-settings-smtp-ssl
                p5.web.widgets.property.get:x:/@_textboxes/*?name
                  value
                p5.web.widgets.property.get:x:/@_checkboxes/*?name
                  checked

                /*
                 * Parametrizing "save settings" invocation.
                 *
                 * Only checkbox values, the rest is forward evaluated below.
                 */
                if:x:/../*/p5.web.widgets.property.get/[1,2]/*/sephia-settings-pop3-ssl/*/checked
                  set:x:/../**/pop3-use-ssl?value
                    src:bool:true
                if:x:/../*/p5.web.widgets.property.get/[1,2]/*/sephia-settings-smtp-ssl/*/checked
                  set:x:/../**/smtp-use-ssl?value
                    src:bool:true

                /*
                 * Checking if this is a "change settings" (not an initial configuration), at which we
                 * allow the user to not supply a password and fingerprint, and resort to using his old passwords.
                 */
                if:x:/@_is-change?value

                  /*
                   * Retrieving old settings, and defaulting all values not explicitly supplied to "old values".
                   */
                  p5.auth.my-settings.get
                  if:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-pop3-password/*?value
                    =:
                    set:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-pop3-password/*?value
                      src:x:/@p5.auth.my-settings.get/*/sephia/*/pop3-password?value
                  if:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-smtp-password/*?value
                    =:
                    set:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-smtp-password/*?value
                      src:x:/@p5.auth.my-settings.get/*/sephia/*/smtp-password?value
                  if:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-gpg-password/*?value
                    =:
                    set:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-gpg-password/*?value
                      src:x:/@p5.auth.my-settings.get/*/sephia/*/gpg-password?value

                /*
                 * Forward evaluating settings values, before we validate that values are legal.
                 */
                eval-x:x:/../*/p5.auth.my-settings.set/*/*

                /*
                 * Except GPG fingerprint, GPG password and email signature, everything is mandatory, unless this is a "change"
                 * operation.
                 */
                if:x:/../*/p5.auth.my-settings.set/*/*(!/gpg-fingerprint!/gpg-password!/signature)/=
                  sys42.windows.info-tip:You must fill out all settings in order for Sephia Five to function correctly.
                    class:info-window info-window-error info-window-longer
                  return

                /*
                 * Saving settings to "auth.hl" file.
                 */
                p5.auth.my-settings.set
                  sephia
                    pop3-server:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-pop3-server/*?value
                    pop3-port:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-pop3-port/*?value
                    pop3-use-ssl:bool:false
                    pop3-username:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-pop3-username/*?value
                    pop3-password:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-pop3-password/*?value
                    smtp-server:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-smtp-server/*?value
                    smtp-port:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-smtp-port/*?value
                    smtp-use-ssl:bool:false
                    smtp-username:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-smtp-username/*?value
                    smtp-password:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-smtp-password/*?value
                    name:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-name/*?value
                    email:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-email/*?value
                    gpg-fingerprint:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-gpg-fingerprint/*?value
                    gpg-password:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-gpg-password/*?value
                    signature:x:/../*/p5.web.widgets.property.get/[0,1]/*/sephia-settings-signature/*?value

                /*
                 * Easy version ...
                 * Simply reloading page, to re-initialize app in its entirety.
                 */
                p5.web.reload-location




/*
 * Checking if caller supplied "change" argument.
 */
if:x:/../*/change?value.bool

  /*
   * Populating with "old settings" (minus passwords).
   */
  p5.auth.my-settings.get:sephia
  p5.web.widgets.property.set:sephia-settings-name
    value:x:/@p5.auth.my-settings.get/*/sephia/*/name?value
  p5.web.widgets.property.set:sephia-settings-email
    value:x:/@p5.auth.my-settings.get/*/sephia/*/email?value
  p5.web.widgets.property.set:sephia-settings-signature
    value:x:/@p5.auth.my-settings.get/*/sephia/*/signature?value
  p5.web.widgets.property.set:sephia-settings-pop3-server
    value:x:/@p5.auth.my-settings.get/*/sephia/*/pop3-server?value
  p5.web.widgets.property.set:sephia-settings-pop3-port
    value:x:/@p5.auth.my-settings.get/*/sephia/*/pop3-port?value
  p5.web.widgets.property.set:sephia-settings-pop3-username
    value:x:/@p5.auth.my-settings.get/*/sephia/*/pop3-username?value
  p5.web.widgets.property.set:sephia-settings-smtp-server
    value:x:/@p5.auth.my-settings.get/*/sephia/*/smtp-server?value
  p5.web.widgets.property.set:sephia-settings-smtp-port
    value:x:/@p5.auth.my-settings.get/*/sephia/*/smtp-port?value
  p5.web.widgets.property.set:sephia-settings-smtp-username
    value:x:/@p5.auth.my-settings.get/*/sephia/*/smtp-username?value
  p5.web.widgets.property.set:sephia-settings-gpg-fingerprint
    value:x:/@p5.auth.my-settings.get/*/sephia/*/gpg-fingerprint?value
  if:x:/@p5.auth.my-settings.get/*/sephia/*/smtp-use-ssl?value.bool
    not
    p5.web.widgets.property.delete:sephia-settings-smtp-ssl
      checked
  if:x:/@p5.auth.my-settings.get/*/sephia/*/pop3-use-ssl?value.bool
    not
    p5.web.widgets.property.delete:sephia-settings-pop3-ssl
      checked

else

  /*
   * Informing user that he must configure his settings before he can proceed.
   */
  sys42.windows.info-tip:You must configure Sephia Five before you can start using it!
  p5.web.widgets.property.set:sephia-settings-close-button
    visible:false
